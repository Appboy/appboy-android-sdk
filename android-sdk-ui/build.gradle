apply plugin: 'android-library'
apply plugin: 'maven-publish'

version = '1.6.0'

version = version + '-ihr'

dependencies {
  compile 'com.appboy:closed-lib:0.0.1'
  // Note: This requires the Android Support Repository to be installed, please see the README.md. The jar is
  // also available in the libs/ directory to help support people who are using older build systems.
  compile 'com.android.support:support-v4:21.0.0'
}

android {
  compileSdkVersion 21
  buildToolsVersion '21.0.0'

  defaultConfig {
    minSdkVersion 8
    targetSdkVersion 21
  }

  sourceSets {
    main.setRoot(".")
    // Note: We'll be moving this project to the new Android Build System conventional folder structure in a future
    // release.
    main {
      manifest.srcFile 'AndroidManifest.xml'
      java.srcDirs = ['src']
      resources.srcDirs = ['src']
      aidl.srcDirs = ['src']
      renderscript.srcDirs = ['src']
      res.srcDirs = ['res']
      assets.srcDirs = ['assets']
    }
  }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        release(MavenPublication) {
            artifact "${project.buildDir}/outputs/aar/${project.name}-release.aar"
            groupId 'com.appboy'
            artifactId 'sdk'

            artifact sourceJar {
                classifier "sources"
            }

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }

    repositories {
        maven {
            url "http://iheart.artifactoryonline.com/iheart/internal"
            credentials {
                username artifactoryUsername
                password artifactoryPassword
            }
        }
    }
}
